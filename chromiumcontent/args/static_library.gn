root_extra_deps = [ "//chromiumcontent:chromiumcontent" ]
is_electron_build = true
is_component_build = false
is_debug = false
symbol_level = 2
enable_nacl = false
enable_widevine = true
proprietary_codecs = true
is_component_ffmpeg = true
ffmpeg_branding = "Chrome"

# Use the system provided standard library on platforms other than Linux.
if (target_os != "linux") {
  use_custom_libcxx = false
}

# Jumbo build should improve compilation times.
# https://chromium.googlesource.com/chromium/src/+/master/docs/jumbo.md
use_jumbo_build = true
if (target_cpu == "arm64") {
  # Temporary disable on arm64 due to "error: '__' macro redefined".
  # Should be enabled back eventually.
  use_jumbo_build = false
}

# This is required by Node.js
# See https://github.com/nodejs/node/pull/13242
v8_promise_internal_field_count = 1

# On ARM64 Linux, use system provided freetype library to prevent this build failure:
# warning: libfreetype.so.6, needed by ../../build/linux/debian_jessie_arm64-sysroot/usr/lib/aarch64-linux-gnu/libfontconfig.so, may conflict with libfreetype.so.6
if (target_cpu == "arm64") {
  use_system_freetype = true
}

# On Windows, we cannot use allocator shim because even though this is
# a non-component build, we still use the dynamic CRT with which the shim
# does not work
if (target_os == "win") {
  use_allocator_shim = false
}

# Configuration for mips64el
if (target_cpu == "mips64el") {
  is_clang = false
  use_allocator = "none"
  mips_arch_variant = "loongson3"
}
